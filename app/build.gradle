apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "vn.behemoth.mycomics"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        multiDexEnabled true // cho phép ứng dụng vượt quá 64k method

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    // Create a variable called keystorePropertiesFile, and initialize it to your
    // keystore.properties file, in the rootProject folder.
    def keystorePropertiesFile = rootProject.file("local.properties")

    // Initialize a new Properties() object called keystoreProperties.
    def keystoreProperties = new Properties()

    // Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    // cấu hình signing cho app
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            // không cho phép debug
            debuggable = false

            // optimize code, bỏ code không sử dụng, check 64k method
            minifyEnabled true

            // bỏ resource không sử dụng
            shrinkResources true

            // chỉ ra đường dẫn đến file proguard.txt
            // file proguard.txt là file chứa các config để obfuscate code code
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }

        debug {
            // cho phép debug
            debuggable = true

            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "default"
    productFlavors {
        beta {
            buildConfigField "vn.behemoth.mycomics.Env", "Env", "vn.behemoth.mycomics.Env.BETA"

            versionCode 10000
            versionName "1.0.005479"
        }

        stable {
            buildConfigField "vn.behemoth.mycomics.Env", "Env", "vn.behemoth.mycomics.Env.STABLE"
        }

        rollout {
            buildConfigField "vn.behemoth.mycomics.Env", "Env", "vn.behemoth.mycomics.Env.ROLL_OUT"
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // multidex
    implementation "androidx.multidex:multidex:2.0.1"

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment:2.1.0'
    implementation 'androidx.navigation:navigation-ui:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}